apply plugin: 'com.android.library'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    publishNonDefault true

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

android.sourceSets.test.java.srcDirs += "build/generated/source/r/debug"

repositories {
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.0.0'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
    testCompile "org.robolectric:robolectric:3.0-rc2"
}

afterEvaluate { project ->

    android.libraryVariants.each { variant ->
        // workaround for missing R class for aar dependencies
        tasks.getByName("assemble${variant.name.capitalize()}").dependsOn copyAppcompat

        // workaround for missing compilation of main sources
        tasks.getByName("test${variant.name.capitalize()}") {
            dependsOn "assemble${variant.name.capitalize()}"
        }
    }
}
 
// another approach to workaround the missing R class for aar dependencies
// which comes from https://github.com/robolectric/robolectric/issues/1737
task copyAppcompat(type: Copy) {
  from 'build/generated/source/r/debug/com/example/core'
  into 'build/generated/source/r/debug/android/support/v7/appcompat'
  include 'R.java'
  filter { line -> line.contains('package com.example.core;') ? 'package android.support.v7.appcompat;' : line }
}
